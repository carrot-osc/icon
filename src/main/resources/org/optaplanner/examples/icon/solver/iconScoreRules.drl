package org.optaplanner.examples.icon;

import java.util.Set;
import java.math.BigDecimal;

import org.optaplanner.core.api.score.buildin.hardsoftbigdecimal.HardSoftBigDecimalScoreHolder;
import org.optaplanner.examples.icon.domain.Resource;
import org.optaplanner.examples.icon.domain.Task;
import org.optaplanner.examples.icon.domain.Machine;
import org.optaplanner.examples.icon.domain.Period;
import org.optaplanner.examples.icon.domain.PeriodPowerCost;

import accumulate org.optaplanner.examples.icon.util.BigDecimalSumAccumulateFunction sumbd;

global HardSoftBigDecimalScoreHolder scoreHolder;

// hard constraints
rule "Resource utilization"
when
    $p: Period()
    $m: Machine()
    $r: Resource()
    $consumption: Number(intValue > 0) from accumulate (
        $t: Task(
            isInitialized(), 
            executor == $m, 
            $p >= startPeriod,
            $p <= finalPeriod
        );
        sum( $t.getResourceConsumption($r))
    )
then
    int capacity = $m.getResourceCapacity($r);
    int cons = $consumption.intValue();
    int result = Math.min(0, capacity - cons);
    BigDecimal overreach = BigDecimal.valueOf(result);
    scoreHolder.addHardConstraintMatch(kcontext, overreach);
end

// objective function

rule "Calculate costs for task power consumption, period by period"
when
    $p: Period()
    $consumption: BigDecimal(this.compareTo(BigDecimal.ZERO) > 0) from accumulate (
        $t: Task(
            isInitialized(), 
            $p >= startPeriod,
            $p <= finalPeriod
        );
        sumbd( $t.getPowerConsumption() )
    )
    $cost: PeriodPowerCost(period == $p)
then
    scoreHolder.addSoftConstraintMatch(kcontext, $consumption.multiply($cost.getCost()).negate());
end

rule "For every machine that has a task, we must count one startup and shutdown"
when
    $m: Machine()
    exists Task(isInitialized(), executor == $m)
then
    BigDecimal result = $m.getCostOnStartup().add($m.getCostOnShutdown()).negate();
    scoreHolder.addSoftConstraintMatch(kcontext, result);
end

rule "When a machine has more than 1 task and an idle period inbetween tasks, we consider it idle"
when
    $m: Machine()
    $p: Period()
    exists Task(isInitialized(), executor == $m)
    accumulate (
        Task(isInitialized(), executor == $m, $start: startPeriod, $end: finalPeriod);
        $min: min($start.getId()),
        $max: max($end.getId())
    )
    not Task(
        isInitialized(),
        executor == $m,
        $p > Period.get($min),
        $p >= startPeriod,
        $p <= finalPeriod,
        $p < Period.get($max)
    )
then
    // FIXME this is stupid; need to figure out how to shutdown/startup instead
    scoreHolder.addSoftConstraintMatch(kcontext, $m.getCostWhenIdle().negate());
end